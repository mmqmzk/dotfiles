#!/usr/bin/env zsh

_preview() {
  local file="$1"
  autoload -Uz has
  if [[ -d "$file" ]] || [[ -h "$file" ]]; then
    local cmd="ls"
    if has exa;
    then
      cmd="exa"
    elif has lsd;
    then
      cmd="lsd"
    fi
    $cmd -lh --color=always "$file"
  elif [[ -f "$file" ]]; then
    local conf="${XDG_CONFIG_HOME:-"$HOME/.config"}"
    local scope="${RANGER_CFG:-"$conf/ranger"}/scope.sh"
    if [[ -f "$scope" ]]; then
      local cache="/tmp/preview"
      [[ ! -d "$cache" ]] && rm -rf "$cache" && mkdir -p "$cache"
      cache="$cache/$(cat /dev/urandom | tr -dc 0-9a-zA-Z | head -q -c 32).png"
      bash "$scope" "$file" "$(($(tput cols)/2))" "$(($(tput lines)/2))" "$cache" "False"
      return 0
    fi
    info="$(file "$file")"
    if [[ "$info" == *text* ]]; then
      if has bat; then
        bat --color=always "$file"
      else
        head -n 50 "$file"
      fi
    else
      echo "$info"
    fi
  fi
}

_preview "$@"
