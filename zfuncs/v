#!/usr/bin/env zsh

export PATH="$HOME/.local/bin:$PATH"

_v() {
  local -a options
  options=("--type" "file")
  local -a query
  local -a editor
  editor=("${EDITOR:-vim}")
  local -a args
  while [[ -n "$1" ]]; do
    case "$1" in
      -h|-H|h|H)
        options+=("--hidden")
        ;;
      -l|-L|l|L)
        options+=("--type" "symlink")
        ;;
      -s|-S|s|S)
        editor=(sudo "$editor")
        ;;
      -*|+*)
        args+=("$1")
        ;;
      *)
        query+=("$1")
        ;;
    esac
    shift
  done
  [[ -e "${query[*]}" ]] && "${editor[@]}" "${args[@]}" "${query[@]}" && return
  [[ -z "${query[*]}" ]] && "${editor[@]}" "${args[@]}" && return
  autoload -Uz has
  if ! has fzf; then
    echo "Tool fzf not installed." && exit 1
  fi
  if ! has fd; then
    echo "Tool fd not installed." && exit 1
  fi
  : "$(fd --color=always $options . . ~ /etc \
    | fzf -1 +m --print-query -0 --query "${query[@]}")"
  [[ -n "$_" ]] && "${editor[@]}" "${args[@]}" "${_//,}"
}

_v "$@"
